     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MEPLOGPF)
     A                                      PRINT
     A                                      INDARA
     A                                      HELP
     A                                      CA03(03 'END OF JOB')
     A                                      CA12(12 'END OF JOB')
     A* Subfile Record Format
     A          R SUBFILE                   SFL
     A                                      TEXT('Subfile Record')
     A*                                     SFLNXTCHG
     A            OPT            1A  B  7 13
     A            BLNAME    R        O  7 17
     A            APP       R        O  7 31
     A* Subfile Control-Recored Format
     A          R CTLFILE                   SFLCTL(SUBFILE)
     A                                      TEXT('Subfile Control-Record')
     A N91                                  ROLLUP(95 'Suite ...')
     A                                      OVERLAY
     A  72                                  SFLDSP
     A  73                                  SFLDSPCTL
     A  71                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  33                                  SFLMSG('Erreur' 33)
     A*           RECNUM         4S 0H      SFLRCDNBR(CURSOR)
     A                                  6 17'Package  :'
     A                                      COLOR(WHT)
     A                                  6 30'Cex :'
     A                                      COLOR(WHT)
     A* Head Format
     A          R HEAD
     A                                      TEXT('En-tête de page')
     A                                      OVERLAY
     A                                  1 34'Visualisation'
     A                                      DSPATR(HI)
     A                                  1 70SYSNAME
     A                                  2 70TIME
     A                                  4  2'1=Sélection 5=Détails 6=Chronologi-
     A                                      e 7=Check'
     A                                      COLOR(BLU)
     A                                  2 30'des packages à traiter'
     A                                      COLOR(WHT)
     A* Foot Format
     A          R FOOT
     A                                      TEXT('Pied de page')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 12'F12=Cancel'
     A                                      COLOR(BLU)
     A* Sous-fichier de messages
     A          R SFLMSG                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ(10)
     A* Sous fichier de controle des messages
     A          R CTLMSG                    SFLCTL(SFLMSG)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N71                                  SFLINZ
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            PGMQ                      SFLPGMQ(10)
