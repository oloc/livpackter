/*********************************************************************/
/* Programme chapeau de récupération des codes objets absents de     */
/* la table des code objet OBJCODTBL. S'il y a des absents,          */
/* on arrête la livraison, on remet le bon de livraison              */
/* en traitement, et on averti l'utilisateur                         */
/*                                                                   */
/* Programmes appelants : MEPMNU, ARCMSGPKG                          */
/*                                                                   */
/* Olivier Locard - Prolival 14/11/06                                */
/* OLOC 09/03/07 Nouveau format des historiques                      */
/* OLOC 03/08/07 Pour éviter les PB de *LIBL, mise en place d'un     */
/* fichier de travail CODANOWF au lieu du master CODANOPF            */
/*********************************************************************/
             PGM        PARM(&BLNAME)

             /* Variables en paramètre                               */
             DCL        VAR(&BLNAME)   TYPE(*CHAR) LEN(10)
             /* Variables du programme                               */
             DCL        VAR(&ALCNBR)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&COMPTEUR) TYPE(*DEC)  LEN(3 0) VALUE(0)
             DCL        VAR(&MBR)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)   TYPE(*CHAR) LEN(132)
             DCL        VAR(&MSGID)    TYPE(*CHAR) LEN(7)
             DCL        VAR(&RETOUR)   TYPE(*CHAR) LEN(4)
             /* Variables pour le endjob                            */
             DCL        VAR(&JOB)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&USR)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&NBR)      TYPE(*CHAR) LEN(6)

             DCLF       FILE(MEPPKGPF)

/*********************************************************************/
/* Historisation du début du traitement des Codes objets absents     */
/*********************************************************************/
             CHGVAR     VAR(&MSGID)  VALUE('MEP0150')
             CHGVAR     VAR(&MSGDTA) VALUE(&BLNAME)
             CALL       PGM(UPDHST)  PARM(&MSGID &MSGDTA)

             /* Création du fichier de réception des CO absents      */
             DLTF       FILE(QTEMP/CODANOWF)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/CODANOWF) SRCMBR(CODANOPF)
             DLTSPLF    FILE(CODANOWF) SPLNBR(*LAST)
             OVRDBF     FILE(CODANOPF) TOFILE(CODANOWF)

             OVRDBF     FILE(MEPPKGPF) TOFILE(&BLNAME)

/*********************************************************************/
/* Boucle sur le bon de livraison afin de déterminer les allocations */
/*********************************************************************/
 BOUCLE:     RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(FINBOUCLE))

             CALL       PGM(UPDCODANO) PARM(&DETTRG &DETTYP &RETOUR)
/* Debug     SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA(&RETOUR) */
             IF         COND(&RETOUR *NE '0000') THEN(DO)
             CHGVAR     VAR(&COMPTEUR) VALUE(&COMPTEUR + 1)
             ENDDO

             GOTO       CMDLBL(BOUCLE)
 FINBOUCLE:  DLTOVR     FILE(MEPPKGPF)
             DLTOVR     FILE(CODANOPF)

             /* Si tous les codes objets sont paramétrés,            */
             /* On historise et on sort du contrôle                  */
             IF         COND(&COMPTEUR *EQ 0) THEN(DO)
             CHGVAR     VAR(&MSGID)  VALUE('MEP0250')
             CHGVAR     VAR(&MSGDTA) VALUE(&BLNAME)
             CALL       PGM(UPDHST)  PARM(&MSGID &MSGDTA)
             RETURN
             ENDDO
 /*******************************************************************/
 /* Tout ce qui suit est dans le cas où on détecte des absents      */
 /*******************************************************************/

/*********************************************************************/
/* Historisation des codes objets absents                            */
/*********************************************************************/
             /* Envoi du nombre de co absents dans l'historique      */
             CHGVAR    VAR(&MSGID)             VALUE('MEP1051')
             CHGVAR    VAR(&ALCNBR)            VALUE(&COMPTEUR)
             CHGVAR    VAR(&MSGDTA)            VALUE(' ')
             CHGVAR    VAR(%SST(&MSGDTA 1 10)) VALUE(&BLNAME)
             CHGVAR    VAR(%SST(&MSGDTA 11 3)) VALUE(&ALCNBR)
             CALL      PGM(UPDHST) PARM(&MSGID &MSGDTA)
             /* Programme qui liste les co absents dans l'historique */
             CALL      PGM(CHKHSTCOD) PARM(&BLNAME)


/*********************************************************************/
/* Historisation de l'arrêt du traitement pour cause des co absents  */
/*********************************************************************/
             CHGVAR     VAR(&MSGID)  VALUE('MEP1050')
             CHGVAR     VAR(&MSGDTA) VALUE(' ')
             CHGVAR     VAR(&MSGDTA) VALUE(&BLNAME)
             CALL       PGM(UPDHST)  PARM(&MSGID &MSGDTA)
             SNDPGMMSG  MSGID(&MSGID) MSGF(MEPMSGF) MSGDTA(&MSGDTA) +
                        MSGTYPE(*ESCAPE)

             ENDPGM
